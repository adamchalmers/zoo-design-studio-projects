// Define the wood case

w = 4
base = startSketchOn(XY)
  |> startProfile(at = [-w / 2, -w / 2])
  |> xLine(length = w, tag = $a)
  |> yLine(length = w, tag = $b)
  |> xLine(length = -w, tag = $c)
  |> yLine(length = -w, tag = $d)
  |> extrude(length = 4)
  |> fillet(
       radius = 0.2,
       tags = [
         getOppositeEdge(a),
         getOppositeEdge(b),
         getOppositeEdge(c),
         getOppositeEdge(d)
       ],
     )

w2 = 3.5
topBase = startSketchOn(base, face = END)
  |> startProfile(at = [-w2 / 2, -w2 / 2])
  |> xLine(length = w2, tag = $a2)
  |> yLine(length = w2, tag = $b2)
  |> xLine(length = -w2, tag = $c2)
  |> yLine(length = -w2, tag = $d2)
  |> close()
  |> extrude(length = 0.4)
  |> fillet(
       radius = 0.2,
       tags = [
         getOppositeEdge(a2),
         getOppositeEdge(b2),
         getOppositeEdge(c2),
         getOppositeEdge(d2)
       ],
     )
  |> appearance(color = "#9a590e")

// Trophy cup (revolved)
gold = "#ffa200"
sketch001 = startSketchOn(-XZ)
profile001 = startProfile(sketch001, at = [-1.55, 4.35])
  |> line(end = [0.9, 0.55])
  |> line(end = [0.28, 0.81])
  |> yLine(length = 0.29)
  |> line(end = [-0.58, 0.99])
  |> line(end = [-1.34, 0.66])
  |> line(end = [-1.35, 4.94])
  |> xLine(length = 0.3)
  |> line(end = [1.32, -3.92])
  |> line(endAbsolute = [0, 8.07])
  |> yLine(length = -3.66)
  |> line(endAbsolute = [profileStartX(%), profileStartY(%)])
  |> close()
  |> revolve(axis = Y)
  |> appearance(color = gold, metalness = 90, roughness = 40)

sketch002 = startSketchOn(XZ)
profile002 = startProfile(sketch002, at = [3.14, 8.21])
  |> line(end = [-0.08, 0.45])
  |> tangentialArc(end = [-0.07, 1.38])
  |> tangentialArc(end = [1.23, 1.19])
  |> tangentialArc(end = [0.12, 1.04])
  |> tangentialArc(end = [-0.39, 0.04])
  |> tangentialArc(end = [-0.44, -0.46])
  |> tangentialArc(end = [0.03, -0.24])
  |> tangentialArc(end = [-0.1, -0.39])
  |> tangentialArc(end = [-0.07, -0.19])
arm = startSketchOn(offsetPlane(XY, offset = 8.21))
|> circle(center = [3.14, 0], radius = 0.09)
|> sweep(path = profile002)
|> patternCircular3d(instances = 2, axis = Z, center = [0, 0, 0])
|> appearance(color = gold, metalness = 90, roughness = 40)