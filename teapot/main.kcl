// Teapot
// A vessel with a spout and a handle used for serving tea and boiling water on a stovetop

// Set units
@settings(defaultLengthUnit = mm)

// Define parameters
teapotHeight = 65
beltlineDiameter = 80
wallThickness = 3
outletHeight = 13
spoutDiameter = 6

// Sketch the path of the teapot spout
spoutPath = startSketchOn(XZ)
  |> startProfile(at = [-beltlineDiameter / 2.5, outletHeight])
  |> xLine(length = -7.525)
  |> tangentialArc(angle = -110, radius = 15, tag = $seg01)
  |> angledLine(angle = tangentToEnd(seg01), length = 8.42)
  |> tangentialArc(angle = 100, radius = 15)

// Sweep a hollow spout
spout = startSketchOn(offsetPlane(YZ, offset = -beltlineDiameter / 2.5))
  |> circle(center = [0, outletHeight], diameter = spoutDiameter)
  |> subtract2d(tool = circle(center = [0, outletHeight], diameter = spoutDiameter * 0.8))
  |> sweep(path = spoutPath)

// Model the perimeter of the teapot body, to be used to trim the handle so that it fits snug to the body
tools = startSketchOn(YZ)
  |> startProfile(at = [0, 0])
  |> xLine(length = beltlineDiameter / 5)
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 2.1,
       teapotHeight / 2.5
     ])
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 3,
       teapotHeight / 1.1
     ])
  |> tangentialArc(endAbsolute = [beltlineDiameter / 4, teapotHeight])
  |> xLine(endAbsolute = profileStartX(%))
  |> line(endAbsolute = profileStart(%))
  |> close()
  |> revolve(axis = Y)

// Sketch the path of the handle
handlePath = startSketchOn(XZ)
  |> startProfile(at = [0, outletHeight])
  |> xLine(endAbsolute = 38)
  |> tangentialArc(end = [6.49, 3.32])
  |> tangentialArc(end = [14.195, 31.555])
  |> tangentialArc(end = [-5.135, 7.4])
  |> tangentialArc(end = [-24.005, 0.905], tag = $seg02)
  |> angledLine(angle = tangentToEnd(seg02), endAbsoluteX = 0)

// Sweep the handle
handle = startSketchOn(YZ)
  |> startProfile(at = [-5, outletHeight + 1.75])
  |> arc(interiorAbsolute = [0, outletHeight + 2.5], endAbsolute = [5, profileStartY(%)])
  |> tangentialArc(end = [0, -3.5])
  |> tangentialArc(end = [-10, 0])
  |> tangentialArc(endAbsolute = profileStart())
  |> close()
  |> sweep(path = handlePath)
  |> subtract(tools)

// Create a cutout in the body for the spout outlet
spoutHole = startSketchOn(YZ)
  |> circle(center = [0, outletHeight], diameter = spoutDiameter)
  |> extrude(length = -beltlineDiameter / 2)

// Model the body of the teapot
body = startSketchOn(YZ)
  |> startProfile(at = [0, 0])
  |> xLine(length = beltlineDiameter / 5)
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 2.1,
       teapotHeight / 2.5
     ])
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 3,
       teapotHeight / 1.1
     ])
  |> tangentialArc(endAbsolute = [beltlineDiameter / 4, teapotHeight])
  |> tangentialArc(angle = 190, diameter = wallThickness)
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 3 - wallThickness,
       teapotHeight / 1.1
     ])
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 2.1 - wallThickness,
       teapotHeight / 2.5
     ])
  |> tangentialArc(endAbsolute = [
       beltlineDiameter / 5,
       profileStartY() + wallThickness
     ])
  |> xLine(endAbsolute = profileStartX(%))
  |> line(endAbsolute = profileStart(%))
  |> close()
  |> revolve(axis = Y)
  |> subtract(tools = [spoutHole])

// Join each component of the teapot and add an appearance
[body, handle, spout]
  |> appearance(color = "#ff0000", metalness = 40, roughness = 30)